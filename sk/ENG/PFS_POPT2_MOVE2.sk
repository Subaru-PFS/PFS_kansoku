#**********************************************************************
#
# POpt2 Movement (Hexapod or ADC)
#
#**********************************************************************
:Header
    SCRIPT_AUTHOR=Moritani
    SCRIPT_UPDATE=2019.08.25

:Parameter
# for PFS
  SSYS=POPT2
# component to move (HEXAPOD or ADC)
  COMP=NONE
# Hexapod movement
  X=!TSCV.PF_OFF_X
  Y=!TSCV.PF_OFF_Y
  Z=!TSCV.PF_OFF_Z
  TX=!TSCV.PF_OFF_TX
  TY=!TSCV.PF_OFF_TY
# ADC movement
## movement select by El. (EL) or Position in mm (MM)
  MMODE=MM
## Elevation
  EL=@status
## Coefficients of ADC position (default is "wave9")
  CONF=WAVE9
## Input position
  POSIN=!STATL.ADCPF_POS

:Command
:start

from "pfsmisc" import adc_pos
#from "math" import fabs

:main_start
if "$COMP" == "HEXAPOD"
    EXEC TSC TUDRIVE MOTOR=ON X=$X Y=$Y Z=$Z TX=$TX TY=$TY;
endif
if "$COMP" == "ADC"
    if "$MMODE" == "EL"
        asn pos = @adc_pos($EL,"$CONF") 
	if (@pos) == (-99)
            asn pos = (!STATL.ADCPF_POS)
        endif
    elif "$MMODE" == "MM"
        asn pos = ($POSIN)
        if (@pos) < (0)
           asn pos = 0
        endif
        if (@pos) > (22)
           asn pos = 22
        endif
    else
        asn pos = (!STATL.ADCPF_POS)
    endif
    EXEC OBS Set_message obsinfo1=clear obsinfo2=clear obsinfo3="ADC Position : @pos" obsinfo4=clear obsinfo5=clear ;
    ## Check current position
    asn posc = (!STATL.ADCPF_POS)
#    if (@fabs(@pos - @posc)) > (0.1)
    asn dpos = (@pos - @posc)
    if (@dpos * @dpos) > (0.01)
        EXEC OBS Set_message obsinfo1=clear obsinfo2=clear obsinfo3=nop obsinfo4="ADC Moves to @pos" obsinfo5=clear ;
        EXEC TSC ADC_PF MOTOR=ON F_SELECT=P_OPT2 COORD=abs POSITION=@pos TELESCOPE=Free ;
        #wait for ADC to finish moving
        asn posu= (@pos + 0.01)
        asn posl= (@pos - 0.01)
        Exec OBS CHECK_STATUS MODE=AND TIMEOUT=90 N1=[STATL.ADCPF_POS @posl @posu] ;
    else
        EXEC OBS Set_message obsinfo1=clear obsinfo2=clear obsinfo3=nop obsinfo4="ADC Position is kept at @posc (diff @dpos)" obsinfo5=clear ;
    endif
endif
:main_end

:end
