#**********************************************************************
#
# PFI rotation center on MCS (M-4, P-4)
#
# -- Rotate Instrument rotator, take exposure, and calculate center
#    Command to send to IIC (not directly to FPS)
#
#**********************************************************************
:Header
    SCRIPT_AUTHOR=Moritani
    SCRIPT_UPDATE=2019.08.26
    OBE_ID=PFS
    OBE_MODE=SPEC_ENG

:Parameter
# for PFS
  SSYS=MCS
##Exposure (leave to IIC)
  NFRAME=2
  EXPTIME=2
#  EXPTYPE=OBJECT
#  DOCENTROID=FALSE
# Telescope movement
  EL=!STATS.EL
#  ROTA=!STATS.IROTPF_POS
  ROTA=-180
  DROTA=30
  NMOVE_ROTA=11
# Object (passed to Gen2 FITS.PFS.OBJECT status.)
  OBJECT="Rotation Center calib"


:Command
:start

# set counters
asn cntrt = 0

# Claim IIC to start measurement
EXEC OBS Set_message obsinfo1=clear obsinfo2="Measure PFI center on MCS" obsinfo3=clear obsinfo4=clear obsinfo5=clear ;
EXEC OBS Set_message obsinfo1=clear obsinfo2=nop obsinfo3="taking data..." obsinfo4=clear obsinfo5=clear ;
EXEC PFS PFSCMD ACTOR="iic" cmd="startBoresightAcquisition expTime=$EXPTIME nExposures=$NFRAME" ;
# Move telescope to initial position and take exposure
EXEC TSC AzElDrive COORD=abs AZ=NOP EL=$EL ,
EXEC TSC InsRot_PF Telescope=free COORD=abs POSITION=$ROTA ;
#*SUB PFS_POPT2_MOVE OBE_ID=PFS OBE_MODE=ENG COMP=ADC MMODE=EL EL=$EL TELESCOPE=free ;

# Take initial exposure
EXEC PFS PFSCMD ACTOR="iic" cmd="addBoresightPosition" ;


:main_start

# Move Instrument rotator and take exposure
while(@cntrt < $NMOVE_ROTA){
    asn cntrt = (@cntrt + 1)
    EXEC TSC InsRot_PF Telescope=free COORD=rel POSITION=$DROTA ;
    EXEC PFS PFSCMD ACTOR="iic" cmd="addBoresightPosition" ;
}
:main_end

EXEC OBS Set_message obsinfo1=clear obsinfo2=nop obsinfo3="calculating..." obsinfo4=clear obsinfo5=clear ;
# Claim IIC to end measurement
EXEC PFS PFSCMD ACTOR="iic" cmd="reduceBoresightData" ;

#EXEC OBS SOUND SELECT=SOUND_LADY_YOSSYA Volume=32 ,
EXEC OBS Set_message obsinfo1=clear obsinfo2=nop obsinfo3=nop obsinfo4=clear obsinfo5="done" ;

EXEC OBS SOUND SELECT=SOUND_LADY_EI Volume=32 ,
# Read Status from IIC about boresight center
# Boresight x , y is ... 
EXEC OBS Set_message obsinfo1=clear obsinfo2=nop obsinfo3=nop obsinfo4="Boresight Center : (!PFS.MCSBORESIGHT_X , !PFS.MCSBORESIGHT_Y)" obsinfo5=nop ;



:end
