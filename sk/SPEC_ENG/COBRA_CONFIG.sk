#**********************************************************************
# Cobra configuration
# usage example: COBRA_CONFIG $DEF_PFSENG OBJECT="TEST" DESIGN_ID="0x20db8bdd9b923899" nIteration=8 tolerance=0.005
#**********************************************************************
:Header
        OBE_ID=PFS
        OBE_mode=SPEC_ENG
        COMMAND=SETUPFIELD
        Script_Version=0.1
        Script_Author=cloomis
        Transcribed=Arai
        Script_Update=2023.07.17 
        # This script was created by extracting cobra config part from SETUPFIELD.sk


:Parameter
	# we default to the last position sent by PFS if the command is repeated
	TARGET = NOP
    DATASET_ID=DS000

    DESIGN_PATH="/data/pfsDesign"
	DESIGN_ID=NOP
    
    # For FITS header
    OBJECT=NOP
    #RA=!STATS.RA
    #DEC=!STATS.DEC
    EQUINOX=!STATS.EQUINOX

    # N_Iteration of Cobra configuration
    CC_NITERATION=8
    #Tolerance
    TOLERANCE=0.005

:Command
:Start

from "pfsmisc" import lower
from "pfsmisc" import random_song

:Main_Start
    *SET TIMELIM = 60

    EXEC pfs pfscmd actor="iic" cmd="declareCurrentPfsDesign designId=$DESIGN_ID" TIMELIM=$TIMELIM ;
    Exec OBS SLEEP SLEEP_TIME=10 ;

    asn DSGN_FNAME=!PFS.DESIGN.NAME
    asn PFSDESIGN_ID=!PFS.DESIGN.ID

    asn INFO1="Cobra Configuration"
    asn INFO2="ID : @PFSDESIGN_ID"
    asn INFO4="Target : @DSGN_FNAME"
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo1=@INFO1 ObsInfo2=@INFO2 ObsInfo4=@INFO4 , 

    # Cobra convergence with Rotator stop
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo3="Cobra configuration with rotator paused: Start." , 
    EXEC TSC InsRot_PF MOTOR=ON Telescope=FREE COORD=abs POSITION=!STATS.IROTPF_POS ;

    ### Pop-up window for checking the fiber illuminator status
    Exec OBS Sound Select=OBCP_STATUS_CHECK Volume=182 ;
    Exec OBS Confirmation Instrument_Name=PFS Title="Are the fiber illuminators on?" Dialog=[OK] ;

    ### Sound during cobraconvergence
    *SUB BGM OBE_ID=PFS OBE_MODE=SPEC_ENG VOLUME=64 ,

    ## Cobra configuration
    EXEC pfs pfscmd actor="iic" cmd="moveToPfsDesign exptime=4.8 nIteration=$CC_NITERATION tolerance=$TOLERANCE" timelim=600;

    ### Pause and wait with a pop-up window --> commented out from 2023 July run.
    #Exec OBS Sound Select=OBCP_STATUS_CHECK Volume=182 ;
    #Exec OBS Confirmation Instrument_Name=PFS Title="Are Cobras already converged?" Dialog=[OK] ;

    # restart rotator move 
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo3="Restart Rotator: Waiting for Rotator to Settle" , 
    EXEC TSC InsRot_PF MOTOR=ON Telescope=LINK COORD=abs POSITION=@ROTA ;
    Exec OBS SLEEP SLEEP_TIME=3 ;
    Exec OBS Check_Status Mode=AND Timeout=0120 N1=[STATS.ROTDIF_PF -0.005 +0.005] ;
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo3="Restart Rotator: Done." ,

:Main_End
:End
