#**********************************************************************
#
# GETOBJECT with dithering for engineering
#   Dither pattern: Hexagonal
#
#**********************************************************************

<Header>
    OBE_ID=PFS
    OBE_mode=SPEC_ENG
    COMMAND=GETOBJECT_ENGDTH_HX
    Script_Author=Takagi
    Script_Update=2022.09.24
</Header>


<Default_Parameter>
    EQUINOX=!STATS.EQUINOX
    DITHWIDTH=5
    EXPTIME=60
    DITH_PAT=[1 0 0 2 -1 0 3 -0.5 0.87 4 0.5 0.87 5 1 0 6 0.5 -0.87 7 -0.5 0.87 8 -1.5 -0.87 9 -2 0 10 -1.5 0.87 11 -1 1.73 12 0 1.73 13 1 1.73 14 1.5 0.87 15 2 0 16 1.5 -0.87 17 1 -1.73 18 0 -1.73 19 -1 -1.73]
    START=1
    STOP=19
    AGEXP=NO
    *AGEXP=YES,NO
    WMODE=YES
    *WMODE=YES,NO
    CORRECT=YES
    OFFSET=REL
    *OFFSET=ABS,REL
</Default_Parameter>


<Command>

:START

### SETUP ###

#Preparation for SPS exposure
*SET EXPTIMLIM = ( $EXPTIME + 60 )

# *SET RA=!STATS.RA
# *SET DEC=!STATS.DEC

*SET RA=!STATS.RA_CMD
*SET DEC=!STATS.DEC_CMD

Exec OBS Set_Message Instrument_Name=PFS ObsInfo1="GetObject: HX Dither" ObsInfo2=Clear ObsInfo3=Clear ObsInfo4=Clear ObsInfo5=Clear ,

Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=0 ;
Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=0 ;

# Wiping SPS
Exec PFS PFSCMD ACTOR="iic" cmd="sps erase" ;


:MAIN_START

# Warning about the fiber illuminator
Exec OBS Sound Select=OBCP_STATUS_CHECK Volume=182 ;
Exec OBS Confirmation Instrument_Name=PFS Title="Is the fiber illuminator off?" Dialog=[OK] ;

# Dithering Loop

*FOR 19 DNUM DRA_WIDTH DDEC_WIDTH IN $DITH_PAT

    *IF ($DNUM) >= ($START) AND ($DNUM) <= ($STOP)

        *IF ($DNUM) == 1
            *Set DRA=0
            *Set DDEC=0
            *Set INFO5="Dither $DNUM DRA=0 sec DDEC=0 sec"
            Exec OBS Set_Message Instrument_Name=PFS ObsInfo5=$INFO5 ;
        *ENDIF

        *IF ($DNUM) > 1

            *IF $OFFSET == ABS
                *SET DRA=($DRA_WIDTH * $DITHWIDTH)
                *SET DDEC=($DDEC_WIDTH * $DITHWIDTH)
                *Set INFO5="Dither $DNUM DRA=$DRA sec DDEC=$DDEC sec"
            *ELIF $OFFSET == REL
                *SET DRA =( ( $DRA_WIDTH - $DRA_PRE )   * $DITHWIDTH )
                *SET DDEC=( ( $DDEC_WIDTH - $DDEC_PRE ) * $DITHWIDTH )
		        *SET DRA_INFO=($DRA_WIDTH * $DITHWIDTH)
		        *SET DDEC_INFO=($DDEC_WIDTH * $DITHWIDTH)
                *Set INFO5="Dither $DNUM DRA=$DRA_INFO  sec DDEC=$DDEC_INFO sec"
            *ENDIF

            Exec OBS Set_Message Instrument_Name=PFS ObsInfo5=$INFO5 ;

            #Calculate Pointing

            *IF $CORRECT == "YES"
                #Calculate AGERR * COS(DEC)
                EXEC OBS CALC COS_ARG=(!STATS.DEC_CMD_DEG) ;
                EXEC OBS CONVSECRADEC RASEC=$DRA-(!STATOBS.COS*!PFS.AG.ERR.RA_ERR) DECSEC=$DDEC-!PFS.AG.ERR.DEC_ERR RABASE=$RA DECBASE=$DEC ;
                EXEC OBS CALC_RADEC MODE=PLUS RABASE=$RA DECBASE=$DEC RAOFFSET=!STATOBS.RARELOUT DECOFFSET=!STATOBS.DECRELOUT ;
                exec OBS memory instrument_name=PFS AG_RA_OFFSET_ASEC=!MEMORY.PFS.AG_RA_OFFSET_ASEC + -1*(!PFS.AG.ERR.RA_ERR) AG_DEC_OFFSET_ASEC=!MEMORY.PFS.AG_DEC_OFFSET_ASEC + -1*(!PFS.AG.ERR.DEC_ERR),
            *ELSE              
                EXEC OBS CONVSECRADEC RASEC=$DRA DECSEC=$DDEC RABASE=$RA DECBASE=$DEC ;
                EXEC OBS CALC_RADEC MODE=PLUS RABASE=$RA DECBASE=$DEC RAOFFSET=!STATOBS.RARELOUT DECOFFSET=!STATOBS.DECRELOUT ;
            *ENDIF
            
            Exec OBS Sleep Sleep_Time=2 ;

            #Slew Telescope
            Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Slewing Telescope" ObsInfo5=$INFO5 ,

            *IF $OFFSET == ABS
                #Telescope offset by ABS
                EXEC TSC TELDRIVE COORD=ABS RA=!STATOBS.RACALCOUT DEC=!STATOBS.DECCALCOUT EQUINOX=$EQUINOX,
                Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=$DRA,
                Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=$DDEC ;
            *ELIF $OFFSET == REL
                #TELESCOPE offset by REL
                EXEC TSC TELDRIVE MOTOR=ON COORD=REL RA=!STATOBS.RARELOUT DEC=!STATOBS.DECRELOUT EQUINOX=!STATS.EQUINOX Direction=TSC ,
                Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=($DRA_WIDTH*$DITHWIDTH),
                Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=($DDEC_WIDTH*$DITHWIDTH) ;
            *ENDIF

            Exec OBS SLEEP SLEEP_TIME=3;
            # Exec OBS Confirmation Instrument_Name=!FITS.SBR.MAINOBCP Title="Check Position." Dialog=[OK] ;
        *ENDIF

        {
        #Exposure
        Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Exposing : $EXPTIME sec" ;
        *IF "$WMODE" == "YES"
            EXEC OBS TIMER DURATION=($EXPTIME) ;
            EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
            EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$EXPTIME window=500,1000 name="SPRDTH_RA:$DRA asec_DEC:$DDEC asec"' TIMELIM=$EXPTIMLIM ;
        *ELSE
            EXEC OBS TIMER DURATION=($EXPTIME) ;
            EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
            EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$EXPTIME name="SPRDTH_RA:$DRA asec_DEC:$DDEC asec"' TIMELIM=$EXPTIMLIM ;
        *ENDIF
        } ;
        #{
        #*IF "$AGEXP" == "YES"
        #    *SUB AGEXP OBE_ID=PFS OBE_MODE=SPEC_ENG EXPTIME=5 NFRAME=1
        #*ENDIF
        #} ;
    
        *SET DRA_PRE=$DRA_WIDTH
        *SET DDEC_PRE=$DDEC_WIDTH
        
    *ENDIF

*ENDFOR



### Go back to home ###

*IF $OFFSET == ABS
    *SET DRA=0
    *SET DDEC=0
*ELIF $OFFSET == REL
    *SET DRA =( ( -1 * $DRA_PRE )  * $DITHWIDTH )
    *SET DDEC=( ( -1 * $DDEC_PRE ) * $DITHWIDTH )
*ENDIF
*Set INFO5="Returning to initial position"


#Calculate Pointing

*IF $CORRECT == "YES"
    #Calculate AGERR * COS(DEC)
    EXEC OBS CALC COS_ARG=(!STATS.DEC_CMD_DEG) ;
    EXEC OBS CONVSECRADEC RASEC=$DRA-(!STATOBS.COS*!PFS.AG.ERR.RA_ERR) DECSEC=$DDEC-!PFS.AG.ERR.DEC_ERR RABASE=$RA DECBASE=$DEC ;
    EXEC OBS CALC_RADEC MODE=PLUS RABASE=$RA DECBASE=$DEC RAOFFSET=!STATOBS.RARELOUT DECOFFSET=!STATOBS.DECRELOUT ;
    exec OBS memory instrument_name=PFS AG_RA_OFFSET_ASEC=!MEMORY.PFS.AG_RA_OFFSET_ASEC + -1*(!PFS.AG.ERR.RA_ERR) AG_DEC_OFFSET_ASEC=!MEMORY.PFS.AG_DEC_OFFSET_ASEC + -1*(!PFS.AG.ERR.DEC_ERR);
*ELSE              
    EXEC OBS CONVSECRADEC RASEC=$DRA DECSEC=$DDEC RABASE=$RA DECBASE=$DEC ;
    EXEC OBS CALC_RADEC MODE=PLUS RABASE=$RA DECBASE=$DEC RAOFFSET=!STATOBS.RARELOUT DECOFFSET=!STATOBS.DECRELOUT ;
*ENDIF

#Slew Telescope
Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Slewing Telescope" ObsInfo5=$INFO5 ,

*IF $OFFSET == ABS
    #Telescope offset by ABS
    EXEC TSC TELDRIVE COORD=ABS RA=!STATOBS.RACALCOUT DEC=!STATOBS.DECCALCOUT EQUINOX=$EQUINOX,
    Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=$DRA,
    Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=$DDEC ;
*ELIF $OFFSET == REL
    #TELESCOPE offset by REL
    EXEC TSC TELDRIVE MOTOR=ON COORD=REL RA=!STATOBS.RARELOUT DEC=!STATOBS.DECRELOUT EQUINOX=!STATS.EQUINOX Direction=TSC ,
    Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=0,
    Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=0 ;
*ENDIF

Exec OBS SLEEP SLEEP_TIME=3;
{
#Exposure
Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Exposing : $EXPTIME sec" ,
*IF "$WMODE" == "YES"
    EXEC OBS TIMER DURATION=($EXPTIME) ;
    EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
    EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$EXPTIME window=500,1000 name="SPRDTH_RA:0 asec_DEC:0 asec"' TIMELIM=$EXPTIMLIM ;
*ELSE
    EXEC OBS TIMER DURATION=($EXPTIME) ;
    EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
    EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$EXPTIME name="SPRDTH_RA:0 asec_DEC:0 asec"' TIMELIM=$EXPTIMLIM ;
*ENDIF
} ;
#{
#*IF "$AGEXP" == "YES"
#    *SUB AGEXP OBE_ID=PFS OBE_MODE=SPEC_ENG EXPTIME=5 NFRAME=1
#*ELSE
#    Exec OBS SLEEP SLEEP_TIME=1;
#*ENDIF
#} ;

Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Done" ;


:MAIN_END

:END

</Command>