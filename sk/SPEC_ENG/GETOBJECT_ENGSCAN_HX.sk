#**********************************************************************
#
# GETOBJECT for scanning (slew the telescope while exposing SpS) for engineering
#   Dither pattern: Hexagonal
#   Dither the telescope by giving the offset to AG (not dithering the telescope from this sk)
#
#**********************************************************************

<Header>
    OBE_ID=PFS
    OBE_mode=SPEC_ENG
    COMMAND=GETOBJECT_ENGSCAN_HX
    Script_Author=Takagi
    Script_Update=2023.07.16
</Header>


<Default_Parameter>
    EQUINOX=!STATS.EQUINOX
    DITHWIDTH=5
    EXPTIME=60
    DITH_PAT=[1 -1 0 2 -0.5 0.87 3 0.5 0.87 4 1 0 5 0.5 -0.87 6 -0.5 -0.87 7 -1.5 -0.87 8 -2 0 9 -1.5 0.87 10 -1 1.73 11 0 1.73 12 1 1.73 13 1.5 0.87 14 2 0 15 1.5 -0.87 16 1 -1.73 17 0 -1.73 18 -1 -1.73]
    START=1
    STOP=18
    WMODE=YES
    *WMODE=YES,NO
    # Camera selection
    CAMERA=NOP
    *CAMERA="b1,r1,n1,b3,r3,n3"
    ARM=NOP
    *ARM="b,r,n"
    SPECNUM=NOP
    *SPECNUM="1,2,3"

</Default_Parameter>


<Command>

:START

from "math" import fabs
from "pfsmisc" import lower
from "pfsmisc" import sps_chk_sel

### SETUP ###

#Preparation for SPS exposure
*SET OPENSHUTTERTIME = ( ( $EXPTIME + 30 ) * 19 )
*SET EXPTIMLIM = ( $EXPTIME + 60 )

*SET RA=!STATS.RA_CMD
*SET DEC=!STATS.DEC_CMD

Exec OBS Set_Message Instrument_Name=PFS ObsInfo1="GetObject: HX Scan" ObsInfo2=Clear ObsInfo3=Clear ObsInfo4=Clear ObsInfo5=Clear ;

# Camera selection options
IF $CAMERA != NOP and @sps_chk_sel("cam", @lower($CAMERA)) ==1
  asn val = @lower($CAMERA)
  asn cmdopt = "cam=@val"
ELIF $ARM != NOP and @sps_chk_sel("arm", @lower($ARM)) ==1
  asn val = @lower($ARM)
  asn cmdopt = "arm=@val"
ELIF $SPECNUM !=NOP and @sps_chk_sel("specNum", @lower($SPECNUM)) ==1
  asn val = @lower($SPECNUM)
  asn cmdopt = "specNum=@val"
ELSE
  asn cmdopt = ""
ENDIF

# Wiping SPS
Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Wiping..." ObsInfo3=Clear ObsInfo4=Clear ObsInfo5=Clear ,
#Exec PFS PFSCMD ACTOR="iic" cmd='sps erase arm=$ARM' ;
Exec PFS PFSCMD ACTOR="iic" cmd='sps erase @cmdopt' ;


# assign GroupID
*SET GROUP_ID=!PFS.GROUPID.ID
Exec OBS Set_Message Instrument_Name=PFS ObsInfo1="GetObject: HX Scan" ObsInfo2=Clear ObsInfo3="GROUP_ID: $GROUP_ID" ObsInfo4=Clear ObsInfo5=Clear ;


:MAIN_START

# Warning about the fiber illuminator
Exec OBS Sound Select=OBCP_STATUS_CHECK Volume=182 ;
Exec OBS Confirmation Instrument_Name=PFS Title="Is the fiber illuminator off?" Dialog=[OK] ;


# Dithering Loop Preparations

*Set DRA=0
*Set DDEC=0
*Set INFO5="Dither 1 DRA=0 sec DDEC=0 sec"
Exec OBS Set_Message Instrument_Name=PFS ObsInfo5=$INFO5 ;

Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=0 ;
Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=0 ;


# Start exposure for scanning

{
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Exposing : $OPENSHUTTERTIME sec";
    *IF "$WMODE" == "YES"
        EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
        #EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$OPENSHUTTERTIME window=500,1000 groupId=$GROUP_ID arm=$ARM name="HX_SCAN_$DITHWIDTH"' TIMELIM=$EXPTIMLIM ;
        EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$OPENSHUTTERTIME window=500,1000 groupId=$GROUP_ID @cmdopt name="HX_SCAN_$DITHWIDTH"' TIMELIM=$EXPTIMLIM ;

    *ELSE
        EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
        #EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$OPENSHUTTERTIME groupId=$GROUP_ID arm=$ARM name="HX_SCAN_$DITHWIDTH"' TIMELIM=$EXPTIMLIM ;
        EXEC PFS PFSCMD ACTOR="iic" cmd='scienceObject exptime=$OPENSHUTTERTIME groupId=$GROUP_ID @cmdopt name="HX_SCAN_$DITHWIDTH"' TIMELIM=$EXPTIMLIM ;

    *ENDIF
},           

{
    *FOR 18 DNUM DRA_WIDTH DDEC_WIDTH IN $DITH_PAT

        *IF ($DNUM) >= ($START) AND ($DNUM) <= ($STOP)
    
            #Staying at the current position 
            EXEC OBS TIMER DURATION=($EXPTIME),
            Exec OBS SLEEP SLEEP_TIME=$EXPTIME ;
            
            {
                *SET DITHPOS=INT($DNUM+1)

                *SET DRA=($DRA_WIDTH * $DITHWIDTH)
                *SET DDEC=($DDEC_WIDTH * $DITHWIDTH)
                *Set INFO5="Dither $DITHPOS DRA=$DRA sec DDEC=$DDEC sec"
                Exec OBS Set_Message Instrument_Name=PFS ObsInfo5=$INFO5 ;
    
                #Send offset to the AG
            	Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=($DRA_WIDTH*$DITHWIDTH),
                Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=($DDEC_WIDTH*$DITHWIDTH) ;

                # Check errors
                Exec OBS Set_Message Instrument_Name=PFS ObsInfo4="Waiting AG to settle down." ;
	            EXEC OBS Sleep Sleep_Time=20 ;
                EXEC OBS CHECK_STATUS MODE=AND TIMEOUT=0180 N1=[PFS.AG.ERR.RA_ERR -0.25 +0.25] N2=[PFS.AG.ERR.DEC_ERR -0.25 +0.25];
                Exec OBS Set_Message Instrument_Name=PFS ObsInfo4=Clear;
            };
        *ENDIF

    *ENDFOR

    #Wait and expose at the last position
    EXEC OBS TIMER DURATION=($EXPTIME),
    EXEC OBS SLEEP SLEEP_TIME=$EXPTIME ;

    #Stop exposure and readout
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Readout..." ,
    EXEC PFS PFSCMD ACTOR="iic" cmd="sps finishExposure now" TIMELIM=100;

};

Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Readout... Done.";


#Go back to the center

*SET DRA=0
*SET DDEC=0
*Set INFO5="Returning to initial position"
Exec OBS Set_Message Instrument_Name=PFS ObsInfo5=$INFO5 ;

#Send offset to the AG
Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_RA_OFFSET_ASEC=0,
Exec OBS MEMORY INSTRUMENT_NAME=PFS DITHER_DEC_OFFSET_ASEC=0 ;

# Check errors
Exec OBS Set_Message Instrument_Name=PFS ObsInfo4="Waiting AG to settle down." ;
EXEC OBS Sleep Sleep_Time=20 ;
EXEC OBS CHECK_STATUS MODE=AND TIMEOUT=0180 N1=[PFS.AG.ERR.RA_ERR -0.25 +0.25] N2=[PFS.AG.ERR.DEC_ERR -0.25 +0.25];

Exec OBS Set_Message Instrument_Name=PFS ObsInfo2="Done" ObsInfo3=Clear ObsInfo4=Clear ObsInfo5=Clear ;


:MAIN_END

:END

</Command>
