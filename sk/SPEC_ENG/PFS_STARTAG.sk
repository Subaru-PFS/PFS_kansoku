#**********************************************************************
#
# Start AG sequence
#   Initialize the AG status and start AG sequence
#   sending commands through AG actor
#
#**********************************************************************
<Header>
    SCRIPT_AUTHOR=Moritani, Yoshida, Koshida, Takagi
    SCRIPT_UPDATE=2021.05.01
    SCRIPT_UPDATE=2022.06.14
    SCRIPT_UPDATE=2023.02.15
    OBE_ID=PFS
    OBE_MODE=SPEC_ENG
</Header>


<Default_Parameter>
#for PFS
  EXPTIME=2
# DESIGN_PATH example: /export/home/pfs/pfsDesign-0x0000000000000001.fits
  DESGIN_PATH="/data/pfsDesign"
#  DESIGN_PATH=NOP
  INTERVAL=10
# DESIGN_ID could be NOP, Declared, or any pfsDesign ID
## This command does not slew the telescope. SetupField is the better choice for start AG with a telescope pointing.
  DESIGN_ID=NOP
# VISIT_ID Default 0
  VISIT_ID=NOP
  FROM_SKY="no"
  DRY_RUN="yes"
  FIT_DSCALE=NOP
  FIT_DINR=NOP

# OTF mode option (if DESIGN_ID==NOP)
  FCENTER=NOP
  *FCENTER="RA,DEC,PA"
  LIMMAG=NOP
  *LIMMAG=20

</Default_Parameter>


<Command>

:start

from "pfsmisc" import lower

# Calc timelimit for sequence of exposure
#*SET TIMELIM = (30 + ($EXPTIME + 15) * $NFRAME)

:main_start

*Set EXPT_AG = INT($EXPTIME * 1000)
*Set INT_AG = INT($INTERVAL * 1000)

EXEC OBS Set_Message Instrument_name=PFS obsinfo1="Auto Guiding: " obsinfo2=clear obsinfo3=clear obsinfo4=clear obsinfo5=clear ;

# Stop running AG process if any
EXEC OBS Set_Message Instrument_name=PFS obsinfo2="Stop running AG" ,

EXEC TSC AG_TRACKING CALC_REGION=PFS MOTOR=OFF ;
EXEC OBS Check_Status Mode=AND Timeout=0030 C1=[STATL.GUIDING NE "YES"] ;
# Check the guideready flag is off
#EXEC pfs pfscmd actor="ag" cmd="autoguide stop" TIMELIM=30 ;
EXEC pfs pfscmd actor="iic" cmd="autoguideStop" TIMELIM=30;
EXEC OBS Check_Status Mode=AND Timeout=0030 N1=[TSCV.PFS.AG.AutoGuideReady -0.005 +0.005] ;

## Field acquisition
#EXEC OBS Set_Message Instrument_name=PFS obsinfo2="Acquire field" ;
#
### pfsDesign mode
IF "$DESIGN_ID" != "NOP"

    ## if pfsDesign should be declared here
    IF "$DESIGN_ID" != "Declared"  
        EXEC pfs pfscmd actor="iic" cmd="declareCurrentPfsDesign designId=$DESIGN_ID" TIMELIM=60 ;
        EXEC OBS SLEEP SLEEP_TIME=10 ;
    ENDIF

    asn DSGN_FNAME=!PFS.DESIGN.NAME
    asn PFSDESIGN_ID=!PFS.DESIGN.ID

    asn INFO3="ID : @PFSDESIGN_ID, Target : @DSGN_FNAME"
    Exec OBS Set_Message Instrument_Name=PFS ObsInfo3=@INFO3 ObsInfo4=Clear ObsInfo5=Clear , 

## On the fly mode
ELSE
#
##    EXEC pfs pfscmd actor="ag" cmd='autoguide start exposure_time=$EXPT_AG cadence=$INT_AG from_sky="yes"' ;
    EXEC OBS Set_Message Instrument_Name=PFS ObsInfo3="On The Fly Mode" ;
#    EXEC pfs pfscmd actor="ag" cmd='acquire_field otf exposure_time=$EXPT_AG cadence=$INT_AG from_sky="no" center=$FCENTER magnitude=$LIMMAG dry_run=$DRY_RUN' #TIMELIM=600 ;
#
ENDIF

# Start AG

*Set INFO4 = "ExpTime : $EXPTIME, Interval : $INTERVAL"
EXEC OBS Set_Message Instrument_name=PFS obsinfo2="Start AG" obsinfo4=INFO4 ;

asn fit_dscale=@lower("$FIT_DSCALE")
asn fit_dinr=@lower("$FIT_DINR")

## pfsDesign mode
IF "$DESIGN_ID" != "NOP"

    IF @fit_dscale == "no" OR @fit_dscale == "yes"
        asn cmdopt1=" fit_dScale=@fit_dscale"
    ELSE
        asn cmdopt1=""
    ENDIF 

    IF @fit_dinr == "no" OR @fit_dinr == "yes"
        asn cmdopt2=" fit_dInR=@fit_dinr"
    ELSE
        asn cmdopt2=""
    ENDIF

    asn cmdopt="@cmdopt1@cmdopt2"

    EXEC pfs pfscmd actor="iic" cmd="autoguideStart exptime=$EXPT_AG cadence=$INT_AG@cmdopt" TIMELIM=60 ;

## On the fly mode
ELSE

    IF @fit_dscale == "no" OR @fit_dscale == "yes"
        asn cmdopt1=" fit_dscale=@fit_dscale"
    ELSE
        asn cmdopt1=""
    ENDIF

    IF @fit_dinr == "no" OR @fit_dinr == "yes"
        asn cmdopt2=" fit_dinr=@fit_dinr"
    ELSE
        asn cmdopt2=""
    ENDIF

    IF "$FCENTER" != NOP
        asn cmdopt3=" center=$FCENTER"
    ELSE
        asn cmdopt3=""
    ENDIF

    IF "$LIMMAG" !=NOP
        asn cmdopt4=" magnitude=$LIMMAG"
    ELSE 
        asn cmdopt4=""
    ENDIF  

    asn cmdopt="@cmdopt1@cmdopt2@cmdopt3@cmdopt4"

    EXEC pfs pfscmd actor="ag" cmd='autoguide start otf exposure_time=$EXPT_AG cadence=$INT_AG dry_run=$DRY_RUN@cmdopt' TIMELIM=60;

ENDIF

## Settle AG

IF "$DRY_RUN" == "yes"

    Exec OBS Confirmation Instrument_Name=PFS Title="Please stabilize AG and start AG loop with launcher. Is that completed?" Dialog=[OK] ;

## Start Telescope guide on
ELIF "$DRY_RUN" == "no"
    Exec OBS Confirmation Instrument_Name=PFS Title="Please check if AG is stable. Is it OK to start AG with Telescope?" Dialog=[OK] ;
    EXEC TSC AG_TRACKING CALC_REGION=PFS MOTOR=ON ;
ENDIF

#EXEC OBS SOUND SELECT=COMMAND_COMPLETE Volume=64 ,
EXEC OBS Set_Message Instrument_name=PFS obsinfo2="AG started. " ;

:main_end

:end

</Command>
