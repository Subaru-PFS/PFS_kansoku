#**********************************************************************
# GETSPSFLAT
#  Take FLAT (with PFI Lamps) with SPS
#  Basically created from PFS_SPS_EXPOSURE.sk
#**********************************************************************

:Header
    SCRIPT_AUTHOR=Takagi, Koshida, Moritani
    SCRIPT_UPDATE=2023.10.09
    OBE_ID=PFS
    OBE_MODE=SPEC
    COMMAND=GETARC


:Parameter

    EXPTYPE=COMPARISON
    NFRAME=1
    # Which camera to be used
    CAMERA="b1,r1,n1,b2,r2,n2,b3,r3,n3,b4,r4"
    ARM=NOP
    *ARM="b,r,n"
    SPECNUM=NOP
    *SPECNUM="1,2,3"
    #SEQ_NAME will be defined afterward 
    SEQ_NAME=NOP
    SEQ_COMMENT=NOP
    #EXPTIME for each lamps. When it is set to > 0, that lamp will turn on during the exposure.
    HGCD=0.0
    AR=0.0
    XE=0.0
    NE=0.0
    KR=0.0

:Command

:start

:main_start

    # Selecting lamps for SEQ_NAME
    IF ($HGCD) > (0.0)
        asn inf_h = "HgCd "
    ELSE
        asn inf_h = ""
    ENDIF

    IF ($AR) > (0.0)
        asn inf_a = "Argon "
    ELSE
        asn inf_a = ""
    ENDIF

    IF ($XE) > (0.0)
        asn inf_x = "Xenon "
    ELSE
        asn inf_x = ""
    ENDIF

    IF ($NE) > (0.0)
        asn inf_n = "Neon "
    ELSE
        asn inf_n = ""
    ENDIF

    IF ($KR) > (0.0)
        asn inf_k = "Krypton "
    ELSE
        asn inf_k = ""
    ENDIF

    asn SEQ_NAME = "Arc: @inf_h@inf_a@inf_x@inf_n@inf_k"

    IF $SEQ_COMMENT != "NOP"
        asn seqcomm = " comments=\"$SEQ_COMMENT\""
    ELSE
        asn seqcomm = ""
    ENDIF

    # Set time limits for commands
    *SET TIMELIM     = ($EXPTIME + 30) 

    *IF ($HGCD) > (0.0)
        *SET TIMELIM_SEQ = 260 + ($EXPTIME + 80) * $NFRAME
    *ELSE
        *SET TIMELIM_SEQ = ($EXPTIME + 85) * $NFRAME
    *ENDIF


    # Camera selection option
    *IF $CAMERA != "NOP"
        *Set cmdopt = "cam=$CAMERA"
    *ELIF $ARM != "NOP"
        *Set cmdopt = "arm=$ARM"
    *ELIF $SPECNUM != "NOP" 
        *Set cmdopt = "specNum=$SPECNUM"
    *ELSE
        *Set cmdopt = ""
    *ENDIF


#     # Check shutter status
#     IF PFS.SHUTTERS.STATE == "Open"
#       raise "error: Shutter status: Open"
#     ELIF PFS.SHUTTERS.STATE == "Unknown"
#       raise "error: Shutter status: Unknown"
#     ELIF PFS.SHUTTERS.STATE == "None"
#       raise "error: Shutter status: None"
#     ENDIF
    
    EXEC OBS Set_Message Instrument_name=PFS obsinfo1=clear obsinfo1="GETARC" ;

    # Start exposure sequence
    {
    EXEC PFS PFSCMD ACTOR="iic" CMD='scienceArc hgcd=$HGCD argon=$AR xenon=$XE neon=$NE krypton=$KR duplicate=$NFRAME $cmdopt name="@SEQ_NAME"@seqcomm' TIMELIM=$TIMELIM_SEQ;
    },

    # Manage Gen2 outputs
    {
    *FOR $NFRAME STEP IN
        #sleep needed?

        EXEC OBS CHECK_STATUS MODE=AND TIMEOUT=180 C1=[PFS.SHUTTERS.STATE EQ open];
        EXEC OBS Set_Message Instrument_name=PFS obsinfo2="$STEP / $NFRAME" obsinfo3="exposing..." ,
        EXEC OBS SOUND SELECT=E_EXPSTART Volume=64 ,
        EXEC OBS TIMER DURATION=$EXPTIME TITLE="Remaining exposure time:";
        EXEC OBS CHECK_STATUS MODE=AND TIMEOUT=$TIMELIM C1=[PFS.SHUTTERS.STATE EQ close];
        EXEC OBS Set_Message Instrument_name=PFS obsinfo3="readout..." ;
    *ENDFOR
    };

    EXEC OBS SOUND SELECT=E_EXPDONE Volume=64 ,
    EXEC OBS Set_Message Instrument_name=PFS obsinfo3="Done" ;

:main_end

:end


