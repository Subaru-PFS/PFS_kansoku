#**********************************************************************
# ROTCORRECTIONLOOP
# Loop to Monitor InsRot error and apply correction when necessary 
#**********************************************************************

:Header
    SCRIPT_AUTHOR=Takagi
    SCRIPT_UPDATE=2024.05.16
    OBE_ID=PFS
    OBE_MODE=SPEC
    COMMAND=ROTCORRECTIONLOOP

:Parameter

    THRESHOLD=5

:Command

:start

from "pfsmisc" import inr_med10err
from "pfsmisc" import gen2_round

:main_start

# set initial parameters to control the loop ON/OFF
EXEC OBS MEMORY INSTRUMENT_NAME=PFS ROTCORLOOP_FLG=1 ;

while 0 < !MEMORY.PFS.ROTCORLOOP_FLG {

    EXEC OBS SLEEP SLEEP_TIME=5;

    # STOP this loop if the telescope autoguiding is not running
    IF !STATL.GUIDING != YES
        EXEC OBS MEMORY INSTRUMENT_NAME=PFS ROTCORLOOP_FLG=0 ,
        EXEC OBS Set_Message Instrument_name=PFS obsinfo4="InsRot error correction loop: Stopped";


    ELSE
        EXEC OBS Set_Message Instrument_name=PFS obsinfo4="InsRot error correction loop: ON";

        # inr_med10err will calculate the INVERSE of median the 10 recent inrerr values.
        # It also will return "0" when the ROTCORRECTIONLOOP is stopped by setting the flag to 0. 
        EXEC OBS MEMORY INSTRUMENT_NAME=PFS INRERR_10MED_INV=(@inr_med10err($THRESHOLD));

        IF !MEMORY.PFS.INRERR_10MED_INV != 0
            asn INR_CORR_F=1/!MEMORY.PFS.INRERR_10MED_INV 
            asn INR_CORR=@gen2_round(@INR_CORR_F, 2)
            EXEC OBS Set_Message Instrument_name=PFS obsinfo4="Correcting InsRot error (@INR_CORR arcsec)...";
            EXEC OBS SLEEP SLEEP_TIME=1;
            Exec OBS Confirmation Instrument_Name=PFS Title="Applying @INR_CORR arcsec offsets, OK?" Dialog=[OK] ;
            *SUB CORRECT_PFS OBE_ID=COMMON OBE_MODE=CMDTEST MODE=ROTATION DELTA_ROT=@INR_CORR ;
            #EXEC TSC INSROT_PF MOTOR=ON TELESCOPE=LINK COORD=REL POSITION=-1*(@INR_CORR / 3600);
            EXEC OBS Set_Message Instrument_name=PFS obsinfo4="Correcting InsRot error (@INR_CORR arcsec)... Done.";

        ELIF !MEMORY.PFS.INRERR_10MED_INV == 0
            EXEC OBS Set_Message Instrument_name=PFS obsinfo4="InsRot error correction loop: Stopped";

	    ENDIF

    ENDIF
}

# while 0 < !MEMORY.PFS.ROTCORLOOP_FLG {
#     
#     IF !MEMORY.PFS.INRERR_10MED_INV != 0
#         asn MED_NOW=1/!MEMORY.PFS.INRERR_10MED_INV
#         EXEC OBS Set_Message Instrument_name=PFS obsinfo4="InsRot error correction loop: ON, current median=@MED_NOW arcsec";
#         EXEC OBS SLEEP SLEEP_TIME=5;
#     ENDIF
# 
#     EXEC OBS SLEEP SLEEP_TIME=2;    
# }

:main_end

:end


